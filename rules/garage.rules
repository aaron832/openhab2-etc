var Timer timerGL = null
var Timer timerLL = null
var Timer timerGFL = null
var Number raceCheck = 0
var Number raceCheckGFL = 0

rule "sensorGarageLastCom_rule"
when
        Item bayDoorGarage01 received update
or
        Item doorGarage01 received update
or
        Item tempGarage01 received update
or
        Item humGarage01 received update
then
        sensorGarageLastCom.postUpdate( new DateTimeType() )
end

rule "garageTempAlarm"
when
        Item tempGarage01 changed
then
        if(tempGarage01.state < 45) {
                sendPushoverMessage(pushoverBuilder("Garage Temperature Low: " + tempGarage01.state).withPriority(0))
                var String timestr = CurrentTime.state.format("%1$tR")
                nodelcdtop.sendCommand(timestr)
                nodelcdbottom.sendCommand("GTemp Low: " + tempGarage01.state)
                nodelcdbright.sendCommand(100)
        }
end

rule "autoGarageLightOnBayDoor"
when
        Item bayDoorGarage01 changed to OPEN 
or
	Item zwave_node4_MotionSensor_BinarySensor changed to ON
then
        raceCheck = 1

        if(timerGL !== null && !timerGL.isRunning() && !timerGL.hasTerminated() && zwave_node3_garagelight_switch.state == ON) {
                logInfo("garage", "Garage light reschedule")
                timerGL.reschedule(now.plusMinutes(2))
        }
        else if(zwave_node3_garagelight_switch.state == OFF) {
                if(timerGL !== null){
                        timerGL.cancel
                        timerGL = null
                }
                timerGL = createTimer(now.plusMinutes(2)) [|
                        zwave_node3_garagelight_switch.sendCommand(OFF)
                ]
                zwave_node3_garagelight_switch.sendCommand(ON)
        }

        if(timerLL !== null && !timerLL.isRunning() && !timerLL.hasTerminated() && zwave_node3_garagelight_switch.state == ON) {
                logInfo("laundry room", "Laundry room light reschedule")
                timerLL.reschedule(now.plusMinutes(2))
        }
        if(zwave_node14_laundryroomlight_switch.state == OFF) {
                if(timerLL !== null){
                        timerLL.cancel
                        timerLL = null
                }
                timerLL = createTimer(now.plusMinutes(2)) [|
                        zwave_node14_laundryroomlight_switch.sendCommand(OFF)
                ]
                zwave_node14_laundryroomlight_switch.sendCommand(ON)
        }
        Thread::sleep(2000)
        raceCheck = 0
end

rule "autoGarageFrontLightOn"
when
        Item bayDoorGarage01 changed to OPEN
or
        Item driveway_camera_alert received command ON
or
        Item door_camera_alert received command ON
then
        if(global_sunisdown.state == OFF) {
                return;
        }
        raceCheckGFL = 1

        if(timerGFL !== null && !timerGFL.isRunning() && !timerGFL.hasTerminated() && zwave_node15_GarageFrontLights_Dimmer.state != 0) {
                logInfo("garage", "Garage Front light reschedule")
                timerGFL.reschedule(now.plusMinutes(15))
        }
        else if(zwave_node15_GarageFrontLights_Dimmer.state == 0) {
                if(timerGFL !== null){
                        timerGFL.cancel
                        timerGFL = null
                }
                timerGFL = createTimer(now.plusMinutes(15)) [|
                        logInfo("garage", "turn off light for autoGarageFrontLightOn")
                        zwave_node15_GarageFrontLights_Dimmer.sendCommand(0)
                ]
                logInfo("garage", "turn on light for autoGarageFrontLightOn")
                zwave_node15_GarageFrontLights_Dimmer.sendCommand(100)
        }

        Thread::sleep(2000)
        raceCheckGFL = 0
end

rule "zwave_node3_garagelight_switch_rule"
when
        Item zwave_node3_garagelight_switch changed
then
        logInfo("garage", "Garage light state: " + zwave_node3_garagelight_switch.state)
        if(timerGL !== null && raceCheck == 0){
                logInfo("garage", "Cancel timerGL")
                timerGL.cancel
                timerGL = null
        }
end

rule "zwave_node14_laundryroomlight_switch_rule"
when
        Item zwave_node14_laundryroomlight_switch changed
then
        logInfo("laundry room", "Laundry light state: " + zwave_node14_laundryroomlight_switch.state)
        //Can check isRunning on timer to see if this is triggered by timer.
        //But there doesn't seem to be a need
        if(timerLL !== null && raceCheck == 0){
                logInfo("laundry room", "Cancel timerLL")
                timerLL.cancel
                timerLL = null
        }
end

//rule "global_outdoor_night_lighting-GarageFrontLights_rule"
//when
//        Item global_outdoor_night_lighting received command
//then
//        if(receivedCommand == ON) {
//                if(timerGFL !== null) {
//                        timerGFL.cancel
//                        timerGFL = null
//                }
//                zwave_node15_GarageFrontLights_Dimmer.sendCommand(100)
//        }
//        else if(receivedCommand == OFF)
//        {
//                if(timerGFL === null || timerGFL.hasTerminated()) {
//                        zwave_node15_GarageFrontLights_Dimmer.sendCommand(0)
//                }
//        }
//end

rule "zwave_node15_GarageFrontLights_Dimmer_rule"
when
        Item zwave_node15_GarageFrontLights_Dimmer changed
then
        logInfo("garage fl", "Garage fl light state: " + zwave_node15_GarageFrontLights_Dimmer.state)
        if(timerGFL !== null && raceCheckGFL == 0){
                logInfo("garage", "Cancel timerGFL")
                timerGFL.cancel
                timerGFL = null
        }
end

